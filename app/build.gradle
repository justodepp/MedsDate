apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.medsdate"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        config
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.config
        }
    }

    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

Properties props = new Properties()
def propFile = new File('gradle.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.config.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.config.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.config.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.config.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'gradle.properties found but some entries are missing'
        android.buildTypes.release.signingConfig = null
    }
}else {
    println 'gradle.properties not found'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$rootProject.xLibVersion"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    // Room
    implementation "androidx.room:room-runtime:$rootProject.xRoomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.xRoomVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.xRoomVersion"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.xLifecycleVersion"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$rootProject.xLifecycleVersion"
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    // Billing
    implementation 'com.android.billingclient:billing:1.1'
    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
    // Showcase
    implementation 'com.github.deano2390:MaterialShowcaseView:1.2.0'
    // Dagger 2
    implementation 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    // Test
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.orhanobut:logger:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

apply plugin: 'com.google.gms.google-services'
